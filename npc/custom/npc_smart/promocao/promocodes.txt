//===== Custom Script ========================================
//= Promotional Codes
//===== By: ==================================================
//= Arzzzae
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= Latest svn
//===== Description: ========================================= 
//= Adds 2 custom atcommands. @claim for players to claim
//= promotional code created by Game Masters. @code is for
//= administrators to add/view/delete promotional codes.
//===== Additional Comments: ================================= 
//= 1.0 - Initial Release.
//============================================================

-	script	promoclaim	-1,{


OnInit:
	bindatcmd "claim",strnpcinfo(3)+"::OnClaim";
	bindatcmd "code",strnpcinfo(3)+"::OnEditCode",99,0;
	end;
	
OnClaim:

	mes "^FF0000 Enter a new promotional code.";
	input .@pc$;
	close2;
	
 
				if ( query_sql("SELECT nameid,amount FROM `reward_codes` WHERE `code`='"+escape_sql(.@pc$)+"'", .@item,.@amt) == 0) {
					dispbottom "Invalid promotional code.";
					end;
			
					}
					
				else if (query_logsql("SELECT code,account_id FROM `rewardlogs` WHERE `code`='"+escape_sql(.@pc$)+"' AND `account_id`='"+getcharid(3)+"'")) {
					dispbottom "You already claimed this promotional code.";
					end;
					
					}

				else {
					getitem .@item,.@amt;
					dispbottom "Promotional code successfuly redeemed.";
					query_logsql("INSERT INTO `rewardlogs` (code,redeem_time,account_id) VALUES ('"+.@pc$+"', NOW(), "+getcharid(3)+")");
					end;
					}
	
		
	
OnEditCode:
	
	menu "Setup a new promotional code",ncode,"View existing codes",vcode,"Delete exisiting codes",dcode,"Cancel",cancel;
	
	ncode:
		
		set .@clength,10; //Promotional Code Length
		
		input .@pcode$;
		
			if (getstrlen(.@pcode$)!=.@clength) {
				dispbottom "Codes must be 10 characters long.";
				end;
				}
				
			mes "You have entered ^FF0000"+.@pcode$+"^000000 as the code.";
			mes "Are you sure?";
			next;
			if(select("Yes:No, thanks")==2) close;
			
			query_sql ("SELECT COUNT(code) FROM `reward_codes` WHERE `code`='"+escape_sql(.@pcode$)+"'", .@pcode_exists);
			if (.@pcode_exists) {
				dispbottom "Sorry, that code already exist.";
				end;
			}
			
			mes "Please set a reward for the players who has entered the code.";
			input .@reward;
			next;
			
			if (.@reward == 0) {
				dispbottom "Sorry, that is an invalid item id.";
				close;
			}
			
			set .@iname$, getitemname(.@reward);
			mes "Item number ^FF0000"+.@reward+"^000000";
			mes "is equivalent to ^FF0000"+.@iname$+"^000000.";
			mes "Are you sure?";
			next;
			if(select("Yes:No, thanks")==2) close;
			
			
			mes "How many ^FF0000"+.@iname$+"^000000.?";
			input .@amount;
			next;
			
			if (.@amount == 0) {
				dispbottom "Sorry, that is an invalid amount number.";
				close;
			}
			
			mes "Are you sure?";
			next;
			if(select("Yes:No, thanks")==2) close;
			
			query_sql ("INSERT INTO `reward_codes` (code,nameid,item_name,amount,time_created) VALUES ('"+.@pcode$+"', "+.@reward+", '"+.@iname$+"', "+.@amount+", NOW())");
			dispbottom "Promotional Code ^FF0000"+.@pcode$+" ^000000is now activated and the reward is ^FF0000"+.@amount+" "+.@iname$+".";
			close;
	
	vcode:
	
		set .@nb, query_sql("SELECT code, item_name, amount FROM `reward_codes` ORDER BY time_created DESC LIMIT 20", .@code$, .@nid$, .@amount);	
		
			if (.@nb == 0) {
			dispbottom "No exisiting codes.";
			end;
			}
		
			dispbottom "=============================================";
			dispbottom "==============  EXISITING CODES  ==============";
			dispbottom "=============================================";
			for(set .@i,0; .@i < .@nb; set .@i,.@i+1)
			dispbottom ""+.@code$[.@i]+" (  Reward: "+.@amount[.@i]+" "+.@nid$[.@i]+"  )";
			dispbottom "=============================================";
			end;
			
	dcode:
	
		input .@dcode$;
		
			query_sql ("SELECT COUNT(code) FROM `reward_codes` WHERE `code`='"+escape_sql(.@dcode$)+"'", .@pcode_exists);
			if (.@pcode_exists) {
				mes "Would you like to delete that code?";
				next;
				if(select("Yes:No, thanks")==2) close;
				
				query_sql ("DELETE FROM `reward_codes` WHERE `code`='"+escape_sql(.@dcode$)+"'");
				dispbottom "Promotional code successfuly deleted.";
				close;
			}
			
			dispbottom "Promotional code not found.";
			end;
	
	cancel:
		close;
}