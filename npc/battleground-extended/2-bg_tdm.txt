// (c) 2008 - 2012 eAmod Project; Andres Garbanzo / Zephyrus
//
//  - gaiaro.staff@yahoo.com
//  - MSN andresjgm.cr@hotmail.com
//  - Skype: Zephyrus_cr
//  - Site: http://dev.terra-gaming.com
//
// This file is NOT public - you are not allowed to distribute it.
// Authorized Server List : http://dev.terra-gaming.com/index.php?/topic/72-authorized-eamod-servers/
// eAmod is a non Free, extended version of eAthena Ragnarok Private Server.

// ==============================================================================
// BattleGround System - Flavius TeamDeathMatch
// ==============================================================================

// Battleground Engine
// *********************************************************************

-	script	Flavius_TD	-1,{
	end;

OnTeam1Quit:
	setquest 8506; // Deserter
	if( $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam1Die:
	if( $@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
	{
		set .Team1_Score, .Team1_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnTeam2Quit:
	setquest 8506; // Deserter
	if( $@BG_Status != 0 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
		donpcevent "BG_Queue_Join::OnJoinEvent";
OnTeam2Die:
	if( $@BG_Status == 1 && getvariableofnpc(.BG_Arena$,"BG_Queue_Join") == "Flavius_TD" )
	{
		set .Team2_Score, .Team2_Score - 1;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;

OnBuildTeams:
	set $@BG_Team1,bg_team_create("bg_tdm",44,99,0,"Flavius_TD::OnTeam1Quit","Flavius_TD::OnTeam1Die",$Palette_BG_Bleu);
	set $@BG_Team2,bg_team_create("bg_tdm",100,43,1,"Flavius_TD::OnTeam2Quit","Flavius_TD::OnTeam2Die",$Palette_BG_Rouge);
	end;

OnReady:
	if( getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD" )
		end;

	initnpctimer;
	set .Team1_Score, 35;
	set .Team2_Score, 35;
	bg_updatescore "bg_tdm",.Team1_Score,.Team2_Score;
	sleep 2000;
	bg_warp $@BG_Team1,"bg_tdm",53,128;
	bg_warp $@BG_Team2,"bg_tdm",146,55;
	sleep 2000;
	bg_team_reveal $@BG_Team1;
	bg_team_reveal $@BG_Team2;
	donpcevent "#guitd_respawn::OnBGStart";
	donpcevent "#crotd_respawn::OnBGStart";
	end;

OnValidateScore:
	if( $@BG_Status != 1 || getvariableofnpc(.BG_Arena$,"BG_Queue_Join") != "Flavius_TD" )
		end;

	bg_updatescore "bg_tdm",.Team1_Score,.Team2_Score;
	set .@Team1Count, bg_get_data($@BG_Team1, 0);
	set .@Team2Count, bg_get_data($@BG_Team2, 0);

	if( .Team2_Score <= 0 )
	{ // Team 1 Won
		callfunc("Reward_BG",15,6,getd("$@" + strnpcinfo(0)));
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .Team1_Score <= 0 )
	{ // Team 2 Won
		callfunc("Reward_BG",6,15,getd("$@" + strnpcinfo(0)));
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else if( .@Team2Count == 0 )
	{ // All Team 2 Players quit
		set .@Rate, 25 - .Team2_Score;
		callfunc("Reward_BG",10 * .@Rate / 25,0,getd("$@" + strnpcinfo(0)));
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .@Team1Count == 0 )
	{ // All Team 1 Players quit
		set .@Rate, 25 - .Team1_Score;
		callfunc("Reward_BG", 0, 10 * .@Rate / 25,getd("$@" + strnpcinfo(0)));
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else end;
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnTimer600000:
	mapannounce "bg_tdm","The Battle will ends in 5 minutes!!",1,0x808000;
	end;

OnTimer840000:
	mapannounce "bg_tdm","The Battle will ends in 1 minute!!",1,0x808000;
	end;

OnTimer900000:
OnPrematureEnd:
	if( .Team1_Score > .Team2_Score )
	{ // Team 1 Won
		callfunc("Reward_BG",15,6,1);
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team1,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team1,4);
	}
	else if( .Team1_Score < .Team2_Score )
	{ // Team 2 Won
		callfunc("Reward_BG",6,15,1);
		mapannounce "bg_tdm","The " + bg_get_data($@BG_Team2,2) + " army has won the Battle of Flavius TD!",1,bg_get_data($@BG_Team2,4);
	}
	else
	{
		callfunc("Reward_BG",6,6,1);
		mapannounce "bg_tdm","The battle is over. This is a Tie...!",1,0x808000;
	}
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnMatchEnd:
	stopnpctimer;
	donpcevent "#guitd_respawn::OnBGStop";
	donpcevent "#crotd_respawn::OnBGStop";
	set $@BG_Status, 2;
	set .Team1_Score, 35;
	set .Team2_Score, 35;
	sleep 5000;
	donpcevent "BG_Queue_Join::OnRotate";
	bg_updatescore "bg_tdm",35,35;
	end;
}

// Battleground Therapist
// *********************************************************************
bg_tdm,35,105,5	script	Therapist in battle#td1	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, take a deep breath";
	mes "and you free yourself from the pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}

bg_tdm,98,35,5	script	Therapist in battle#td2	95,{
	mes "[Therapist in battle]";
	mes "Just close your eyes, take a deep breath";
	mes "and you free yourself from the pain.";
	specialeffect2 312;
	percentheal 100,100;
	repairall;
	close2;
	openstorage;
	end;
}


// Battleground Respawn
// *********************************************************************
bg_tdm,44,99,0	script	#guitd_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bg_tdm",40,98,47,105,100,100;
	areawarp "bg_tdm",40,98,47,105,"bg_tdm",50,131,77,102;
	initnpctimer;
	end;
}

bg_tdm,100,43,0	script	#crotd_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bg_tdm",96,42,103,49,100,100;
	areawarp "bg_tdm",96,42,103,49,"bg_tdm",122,71,149,52;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bg_tdm,40,105,1	duplicate(Base Flag#bg)	Alpha Base#td_1	973
bg_tdm,47,105,1	duplicate(Base Flag#bg)	Alpha Base#td_2	973

bg_tdm,96,49,1	duplicate(Base Flag#bg)	Omega Base#td_1	974
bg_tdm,103,49,1	duplicate(Base Flag#bg)	Omega Base#td_2	974

bg_tdm,76,102,1	duplicate(Base Flag#bg)	Alpha Base#td_3	973
bg_tdm,89,111,1	duplicate(Base Flag#bg)	Alpha Base#td_4	973
bg_tdm,61,130,1	duplicate(Base Flag#bg)	Alpha Base#td_5	973
bg_tdm,51,120,1	duplicate(Base Flag#bg)	Alpha Base#td_6	973

bg_tdm,100,72,1	duplicate(Base Flag#bg)	Omega Base#td_3	974
bg_tdm,123,81,1	duplicate(Base Flag#bg)	Omega Base#td_4	974
bg_tdm,138,53,1	duplicate(Base Flag#bg)	Omega Base#td_5	974
bg_tdm,148,63,1	duplicate(Base Flag#bg)	Omega Base#td_6	974

// MapFlags
// *********************************************************************

bg_tdm	mapflag	battleground	2
bg_tdm	mapflag	nomemo
bg_tdm	mapflag	nosave	SavePoint
bg_tdm	mapflag	noteleport
bg_tdm	mapflag	nowarp
bg_tdm	mapflag	nowarpto
bg_tdm	mapflag	noreturn
bg_tdm	mapflag	nobranch
bg_tdm	mapflag	nopenalty
bg_tdm	mapflag	nodrop
